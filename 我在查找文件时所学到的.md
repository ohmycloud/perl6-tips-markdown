[Things I found out while finding](https://gfldex.wordpress.com/2016/10/02/things-i-found-out-while-finding/)

[Concurrent::File::Find](https://github.com/gfldex/perl6-concurrent-file-find) 现在已经在[生态系统](https://github.com/perl6/ecosystem)中了。我在这里分享几点我学到的东西。

在子签名(sub-signature)上使用 where 从句将不会在子签名中提供变量。 如果 where 从句必须对所有参数进行操作，为了检查排他性或其他相互依赖性，那么它必须应用于最后一个参数上。 这个限制设计时就是这样的，所以我告诉了[文档](https://github.com/perl6/doc/commit/1f30c682a8486dde594bef6dc05af9ab97f78fa6)。 where 从句对捕获没有效果，但原意[不是](https://rt.perl.org/Public/Bug/Display.html?id=129430)那样设计的并且第二天 [jnthn](http://jnthn.net/) 就修复了它。 我知道抱怨会得到你的关注，但我没想到它是这么快。

我还发现一个叫 `.close` 的方法, 它几乎总是需要一个 `LEAVE`。 让我们看一些代码。

```perl6
sub find-simple ( IO(Str) $dir,
    :$keep-going = True,
    :$no-thread = False
) is export {
    my $channel = Channel.new;

    my &start = -> ( &c ) { c } if $no-thread;

    my $promise = start { 
        for dir($dir) {
            CATCH { default { if $keep-going { note .Str } else { .rethrow } } }
            
            if .IO.l && !.IO.e {
                X::IO::StaleSymlink.new(path=>.Str).throw;
            }
            {
                CATCH { when X::Channel::SendOnClosed { last } }
                $channel.send(.IO) if .IO.f;
                $channel.send(.IO) if .IO.d;
            }
            .IO.dir()».&?BLOCK if .IO.e && .IO.d;
        }
        LEAVE $channel.close unless $channel.closed;
    }

    return $channel.list but role :: { method channel { $channel } };
}
```

这实际上接收由 `dir` 返回的列表(List)，并通过[Channel](https://docs.perl6.org/type/Channel)通道返回文件和目录的 `IO::Path`。 对于任何目录，它会递归到 for-block 中（这就是 `».&?BLOCK` 的意思）。 因为那些[文件测试](https://docs.perl6.org/type/IO$COLON$COLONPath#File_test_operators)中的任何一个都可能触发异常，所以我们可能会离开那个 `start-block`。 如果有一个 Promise，那将在 return 语句背后给我们发回，那里什么也没有。 返回语句实际上在主线程中，并且一些消费者(consumer)将阻塞返回的 `.list`。

```perl6
.say for find-simple('/home/you');
```

对于那个通道(Channel)的消费者(consumer)来说，他并不能清楚地知道他可能会被永远地阻塞。 因此如果出现错误, `LEAVE` 语句能确保通道(Channel)被关闭。

将角色混合到 `.listified` 的通道(Channel)中提供了一个侧通道(side channel)，以把通道(Channel)交给消费者(consumer)，因此可以在该端关闭。

```perl6
my @l := find-simple(%*ENV<HOME>, :keep-going); # binding to avoid eagerness

for @l {
    @l.channel.close if $++ > 5000; # hard-close the channel after 5000 found files
    .say if $++ %% 100 # print every 100th file
}
```

绑定是必需的，否则那个数组会吃掉整个通道(Channel)，而如果在数组那个位置放一个标量, 我们必须展平它。 请注意匿名状态变量以最不可能的方式计数是多么好。 这是能一起玩的大量 Perl 6 特性的情况之一。 Sigils 允许指示一个变量，即使它没有名字。   通过 `postfix:<++>` 在 Any 上进行 Autovivication  会得到一个0，它会增加到1. 因为我们没有一个溢出到本地作用域（第二个 `$++` 引入一个单独的变量）的名字， 所以我们不需要声明符。 我从来不喜欢短变量，因为他们在移动代码很容易被漏掉。 然后编译器会抱怨缺少声明，或者更糟的是，变量的初始化值是错误的。

但有问题。 在测试一个干净的虚拟机（Debian Jessie）的时候，我发现 symlink-loops（/sys / 有很多这种东西）会导致 Promised 的线程消耗 100% 的 CPU，而主线程什么都不做。 看起来好像是线程有毛病。 也许结合大量的异常。 我不知道是否会有一个错误。 没有人在合成测试中测试异常垃圾邮件。

这就是 `$no-thread` 要做的 仅仅把 `&start` 声明为一个几乎空的点，它将禁用任何线程，并把通道(Channel)变成一个荣耀的数组。

无论如何，写和使用并发代码真的很容易，只要我们通过一个通道(Channel)将 `gather/take` 转换为 `start/send` 来传输一个列表。

**更新**：我只是设法打**[高尔夫](https://gist.github.com/f76cb5e18309eaf8fdf9bf8b458c812e)**并**[报告](https://rt.perl.org/rt3//Public/Bug/Display.html?id=129787)**错误。 在你读到这儿的时候，它可能已经被修复了。















